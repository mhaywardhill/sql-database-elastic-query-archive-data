@description('SQL Server name (must be globally unique)')
param sqlServerName string

@description('Azure region')
param location string

@description('SQL administrator login')
param adminLogin string

@description('SQL administrator password')
@secure()
param adminPassword string

@description('Allow Azure services to access this server (required for elastic query)')
param allowAzureServices bool = true

resource sqlServer 'Microsoft.Sql/servers@2023-08-01-preview' = {
  name: sqlServerName
  location: location
  properties: {
    administratorLogin: adminLogin
    administratorLoginPassword: adminPassword
    minimalTlsVersion: '1.2'
    publicNetworkAccess: 'Enabled'
  }
}

resource allowAzureServicesRule 'Microsoft.Sql/servers/firewallRules@2023-08-01-preview' = if (allowAzureServices) {
  parent: sqlServer
  name: 'AllowAllWindowsAzureIps'
  properties: {
    startIpAddress: '0.0.0.0'
    endIpAddress: '0.0.0.0'
  }
}

output sqlServerId string = sqlServer.id
output sqlServerName string = sqlServer.name
